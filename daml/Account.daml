-- Copyright (c) 2021 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

-- This module implements an account for holding assets.
-- An AssetHoldingAccount can hold only one type of asset. A separate AssetHoldingAccount needs to be created for each type of asset.
-- Account creation follows propose/accept pattern implemented through AssetHoldingAccount template with
-- Invite_New_Asset_Holder choice, that creates AssetHoldingAccountProposal contract. 
-- AssetHoldingAccountProposal_Accept choice creates new AssetHoldingAccount template with the
-- proposal recipient party as the account owner.
module Account where

import DA.Action
import Asset

-- AssetHoldingAccount represents an account, where Asset contracts can be held
-- Assets held in the account are all Asset contracts from Active Contract Set,
-- where assetType and owner match between Asset and AssetHoldingAccount.
-- The sum of the amounts of the assets held in the account represents the account balance.
template AssetHoldingAccount
  with
    -- Combination of issuer, symbol, fungible and reference properties define the asset type that can be held in the account.
    assetType : AssetType
    -- The owner of the account.
    owner : Party
    -- Specifies whether the issuer can create the Asset in owner's account.
    -- Since the Asset contract is signed by both issuer and owner, the issuer cannot unilaterally
    -- create an Asset, where owner/=issuer, by exercising "create Asset" command. 
    -- With airdroppable account the issuer can create Asset in owner's account by exercising
    -- Ardrop choice on the owner's AssetHoldingAccount contract.
    airdroppable : Bool
    -- Specifies whether only the issuer can invite parties to create AssetHoldingAccount
    -- or both issuer and owner have this privilege.
    resharable : Bool 
  where
    signatory assetType.issuer, owner

    key (assetType, owner) : (AssetType, Party)
    maintainer key._1.issuer

    -- Send an invite for a party to create AssetHoldingAccount for this asset type.
    nonconsuming choice Invite_New_Asset_Holder : ContractId AssetHoldingAccountProposal
      with
        recipient : Party
      controller (if resharable then owner else assetType.issuer) 
      do
        create AssetHoldingAccountProposal with
          account = this
          recipient

    -- A choice for issuer to aidrop the Asset into owner's account.
    -- This choice can also be used by the issuer to mint asset into own account.
    nonconsuming choice Airdrop : ContractId Asset
      with
        amount : Decimal
      controller assetType.issuer 
      do
        assertMsg "Cannot create airdrop. The target account does not accept airdrops" (airdroppable || assetType.issuer==owner)
        assertMsg "Cannot create airdrop. The amount of non fungible asset must be 1.0" (assetType.fungible || amount==1.0)
        create Asset with
          assetType
          owner
          amount

    -- The purpose of this choice is to allow client application to create a transfer proposal using multiple Asset 
    -- contracts held in the account. Client application can query for all active Asset contracts, where assetType and owner 
    -- match between Asset and AssetHoldingAccount, which represent assets held in the account.
    -- Then the list of Asset contract Ids can be passed to this choice allowing the transfer proposal to be created
    -- as long as the transfer amount is less than the account balance.
    nonconsuming choice Create_Transfer : ContractId AssetTransfer
      with
        assetsCids : [ContractId Asset]
        amount : Decimal
        recipient : Party
      controller owner 
      do
        assertMsg "Cannot create transfer. The amount of non fungible asset must be 1.0" (assetType.fungible || amount==1.0)
        totAssetAmount <- sum . map (\x -> x.amount) <$>  forA assetsCids (\assetCid -> do
          asset <- fetch assetCid
          assertMsg "Cannot create transfer. Asset type mismatch between asset and account" (asset.assetType == assetType)
          assertMsg "Cannot create transfer. Owner mismatch between asset and account" (asset.owner == owner)
          archive assetCid
          return asset)
        when (totAssetAmount > amount)(do 
          create Asset with
            assetType
            owner
            amount = totAssetAmount - amount
          return ())
        transferAsset <- create Asset with
          assetType
          owner
          amount
        exercise transferAsset Asset_Transfer with recipient
        

template AssetHoldingAccountProposal
  with
    account : AssetHoldingAccount
    recipient : Party
  where
    signatory account.assetType.issuer
    observer recipient

    choice AssetHoldingAccountProposal_Accept : ContractId AssetHoldingAccount
      controller recipient 
      do
        create account with
          owner = recipient

    choice AssetHoldingAccountProposal_Reject : ()
      controller recipient 
      do
        return ()


